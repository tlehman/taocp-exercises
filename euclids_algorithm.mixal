* Program E: An implementation of Euclid's Algorithm
* by tlehman on 1380146870, first full MIX program
*
* The idea behind thef algorithm comes from the mathematical fact that
* gcd(m, n) = gcd(n, r), this is where the reduction step comes from.
* 	      	    (r = m mod n)
*
* E1. [Find remainder]
*    r <- m mod n
* E2. [Is it zero?] if r=0, terminate
* E3. [Reduce]
*    m <- n
*    n <- r (m mod n)
* 
* This program will use the values m=165, n=90, where gcd(m,n)=15
*
* rX  <- m
* rI1 <- n

PRNT  EQU	19	* Typewriter terminal, stdout in MDK
DVSR  EQU	100	* store n for DIV operation
SWAP  EQU	101	* swap to handle reg-to-reg transfers (inefficient)
START ENTA	0
      ENTX	165
      ENT1	90
E1    ST1	DVSR	* store n in DVSR
      DIV	DVSR	* rA <- m//n; rX <- m%n
E2    CMPX	=0=
      JE	QUIT	* halt if r=0, then CONTENTS(DVSOR)=gcd(m,n)
E3    STX	SWAP
      LD1	SWAP	* n <- r
      LDX	DVSR	* m <- n
      ENTA	0
      JMP	E1	* go to E1
QUIT  LDA	DVSR    * rA <- gcd(m,n)
      CHAR	0       * convert rA to character code
      STX	DVSR	* store least significant digits in DVSR cell
      OUT	DVSR(PRNT)
      HLT
      END	START
